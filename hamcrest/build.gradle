buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

version '0.3.0'

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.android.tools.lint:lint-tests:24.3.1'
    compile group: 'org.hamcrest', name: 'hamcrest-core', version: '1.3'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintray_user') ? bintray_user : ''
    key = project.hasProperty('bintray_key') ? bintray_key : ''
    publications = ['mavenJava']
    dryRun = false
    publish = false
    pkg {
        repo = 'maven'
        name = 'de.ad:lint-hamcrest'
        desc = 'Hamcrest matchers for Android Lint Warnings.'
        websiteUrl = 'https://github.com/a11n/lint-junit-rule'
        issueTrackerUrl = 'https://github.com/a11n/lint-junit-rule/issues'
        vcsUrl = 'https://github.com/a11n/lint-junit-rule.git'
        licenses = ['MIT']
        labels = ['Android', 'lint', 'JUnit', 'AssertJ']
        publicDownloadNumbers = false
        version {
            name = project.version
            desc = ''
        }
    }
}
